set( externalComponents "" )
set(active OFF)
file( GLOB children RELATIVE ${CMAKE_CURRENT_LIST_DIR} ${CMAKE_CURRENT_LIST_DIR}/* )
foreach( child ${children} )
  if( IS_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/${child} )
    message( STATUS "Found directory at src/externalComponents/${child}" )
    string(TOUPPER ${child} option)
    option(ENABLE_${option} "Build the ${name}" ${active})
    if( ${ENABLE_${option}} )
      if( (EXISTS "${CMAKE_CURRENT_LIST_DIR}/${child}/CMakeLists.txt") )
        message( STATUS "Found CMakeLists.txt at src/externalComponents/${child}....adding component!" )
        add_subdirectory( ${child} )
        list( APPEND externalComponents ${child} )
      else()
        message( FATAL_ERROR "ENABLE_${option} is ON, but cannot find CMakeLists.txt" )
      endif()
    endif()
  endif()
endforeach()

set( externalComponentsList "${externalComponents}" CACHE STRING "" FORCE )
set( externalComponentsLinkList "" )

if( ${BUILD_SHARED_LIBS} )
  set( externalComponentsLinkList ${externalComponentsList} )
else()
 foreach(component ${externalComponentsList})
   set( externalComponentsLinkList ${externalComponentsLinkList} ${GEOS_LINK_PREPEND_FLAG} ${component} ${GEOS_LINK_POSTPEND_FLAG} )
 endforeach()
endif()

set( externalComponentsLinkList ${externalComponentsLinkList} PARENT_SCOPE )
